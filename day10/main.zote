include!("stdlib");
include!("../aoc.zote");

machines := "input.txt" >> read >> split("\n") >> map(\l -> {
    b1 := l >> find("]");
    b2 := l >> find("{");
    [
        l[1:b1] >> rev >> foldl(0, \acc, c -> acc*2 + (c == "#")),
        l[b1:b2-2] >> split(")") >> map(\>> ints >> map(\p -> 2^p) >> sum),
        l[b2:] >> ints, // TODO
    ]
});

machines >> map(\(pattern, buttons, _) -> {
    vis := set();
    states := [0];
    presses := 0;
    while states {
        presses += 1;
        new := [];
        for state in states {
            for button in buttons {
                next := state >> bit_xor(button);
                if next == pattern return presses;
                if next >> in(vis) >> not
                    next >> insert(vis) >> push(new);
            }
        }
        states = new;
    }
    nil
}) >> filter(\>> neq(nil)) >> sum >> print;
